plugins {
    id 'java'
    id 'java-library'
    id 'java-test-fixtures'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    apply plugin: 'java-library'

    group = 'Roadeye'
    version = '0.0.1'

    dependencies {
        implementation 'org.jspecify:jspecify:1.0.0'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.mockito:mockito-subclass'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    repositories {
        mavenCentral()
    }

    tasks.named('test') {
        jvmArgs += "-Xshare:off"
        jvmArgs += "-XX:+EnableDynamicAgentLoading"
        jvmArgs += "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}"

        useJUnitPlatform()
    }
}

subprojects {
    apply plugin: 'java-test-fixtures'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    bootJar.enabled = true
    jar.enabled = true
    dependencies {}

    dependencyManagement {
        dependencies {
            dependencySet("io.github.openfeign.querydsl:7.0") {
                entry("querydsl-apt")
                entry("querydsl-core")
                entry("querydsl-jpa")
            }
        }
    }
}
